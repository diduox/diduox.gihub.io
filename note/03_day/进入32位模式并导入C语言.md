#### 进入32位模式并导入C语言

##### 1.制作真正的IPL

Initial Program Loader，启动程序装载器，也叫做启动区

```assembly
; haribote-ipl
; TAB=4

		ORG		0x7c00			; 指明程序的装载地址

; 以下这段是标准FAT12格式磁盘专用的代码

		JMP		entry
		DB		0x90
		DB		"HELLOIPL"		; 启动区的名称可以是任意的字符串（8字节）
		DW		512				; 每个扇区的大小（必须为512字节）
		DB		1				; 簇的大小（必须为一个扇区1）
		DW		1				; FAT的起始位置（一般从第一个扇区开始）
		DB		2				; FAT的个数（必须为2）
		DW		224				; 根目录的大小（一般设为224项）
		DW		2880			; 该磁盘的大小（必须是2880扇区）
		DB		0xf0			; 磁盘的种类（必须是0xf0）
		DW		9				; FAT的长度（必须是9扇区）
		DW		18				; 1个磁道（track）有几个扇区（必须是18）
		DW		2				; 磁头数（必须是2）
		DD		0				; 不使用分区，必须是0
		DD		2880			; 重写一次磁盘大小
		DB		0,0,0x29		; 意义不明，固定码
		DD		0xffffffff		; （可能是）卷标号码
		DB		"HELLO-OS   "	; 磁盘的名称（11字节）
		DB		"FAT12   "		; 磁盘格式名称（8字节）
		RESB	18				; 先空出18字节

; 程序主体

entry:
		MOV		AX,0			; 初始化寄存器
		MOV		SS,AX
		MOV		SP,0x7c00
		MOV		DS,AX

; 读磁盘 此为IPL新增部分 因为磁盘最初的512字节是启动区，所以要装载下一个字节的内容
; 第一个扇区已经默认被加载和执行，而第二个扇区的内容则通过这段代码主动读取并处理
; 现在执行的就是第一个扇区的内容
		MOV		AX,0x0820
		MOV		ES,AX			; ES = 0x0820 地址的表示方式是 EX*16 + BX 这是指定将软盘的数据加载到内存的哪里
 		MOV		CH,0			; 柱面0
		MOV		DH,0			; 磁头0
		MOV		CL,2			; 扇区2

		MOV		AH,0x02			; AH=0x02 : 读盘
		MOV		AL,1			; 1个扇区
		MOV		BX,0
		MOV		DL,0x00			; A驱动器 指定驱动器号 如果只有一个驱动器的话 直接写0就可以了
		INT		0x13			; 调用磁盘BIOS
		JC		error

;虽然读完了，但总之没什么要做的，所以睡觉

fin:
		HLT						; 让CPU停止，等待指令
		JMP		fin				; 无限循环

error:							;只有当有错误的时候 才会打印出Hello,world
		MOV		SI,msg
putloop:
		MOV		AL,[SI]
		ADD		SI,1			; 给SI加1
		CMP		AL,0
		JE		fin
		MOV		AH,0x0e			; 显示一个文字
		MOV		BX,15			; 指定字符颜色
		INT		0x10			; 调用显卡BIOS
		JMP		putloop
msg:
		DB		0x0a, 0x0a		; 换行两次
		DB		"hello, world"
		DB		0x0a			; 换行
		DB		0

		RESB	0x7dfe-$		; 填写0x00,直到0x001fe

		DB		0x55, 0xaa

```

> 磁盘读、写，扇区校验（verify），以及寻 道（seek） 
>
> AH=0x02;（读盘） 
>
> AH=0x03;（写盘） 
>
> AH=0x04;（校验） 
>
> AH=0x0c;（寻道） AL=处理对象的扇区数;（只能同时处 理连续的扇区） 
>
> CH=柱面号 &0xff; CL=扇区号（0-5位）|（柱面号 &0x300）* * 2;
>
>  DH=磁头号; 
>
> DL=驱动器号； 
>
> ES:BX=缓冲地址；(校验及寻道时不 使用) 返回值： 
>
> FLACS.CF==0：没有错误， AH==0 
>
> FLAGS.CF==1：有错误，错误号码 存入AH内（与重置（reset）功能一 样）

##### 2.试错 如果发生错误 重新读入

```assembly
;读磁盘
	MOV AX,0x0820
	MOV ES,AX
	MOV CH,0 ; 柱面0
	MOV DH,0 ; 磁头0
	MOV CL,2 ; 扇区2
	MOV SI,0 ; 记录失败次数的寄存器
retry:
	MOV AH,0x02 ; AH=0x02 : 读入磁盘
	MOV AL,1 ; 1个扇区
	MOV BX,0
	MOV DL,0x00 ; A驱动器
	INT 0x13 ; 调用磁盘BIOS
	JNC fin ; 没出错的话跳转到fin
	ADD SI,1 ; 往SI加1
	CMP SI,5 ; 比较SI与5
	JAE error ; SI >= 5时，跳转到error
	MOV AH,0x00	;进行系统复位 复位软盘状态
	MOV DL,0x00 ; A驱动器
	INT 0x13 ; 重置驱动器
	JMP retry
```

JNC指令：”jump if not carry“ 如果进位标志是0的话，就跳转

JAE指令：”jump if above or equal“ 大于等于时跳转

##### 3.读到18扇区

```assembly
;读磁盘
	MOV AX,0x0820
	MOV ES,AX
	MOV CH,0 ; 柱面0
	MOV DH,0 ; 磁头0
	MOV CL,2 ; 扇区2
readloop:
	MOV SI,0 ; 记录失败次数的寄存器
retry:
	MOV AH,0x02 ; AH=0x02 :读入磁盘的BIOS指令编号
	MOV AL,1 ; 1个扇区 AL代表一次读入几个扇区 
	MOV BX,0
	MOV DL,0x00 ; A驱动器
	INT 0x13 ; 调用磁盘BIOS
	JNC next ; 没出错时跳转到next
	ADD SI,1 ; 往SI加1
	CMP SI,5 ; 比较SI与5
	JAE error ; SI >= 5时，跳转到error
	MOV AH,0x00
	MOV DL,0x00 ; A驱动器
	INT 0x13 ; 重置驱动器
	JMP retry
next:
	MOV AX,ES ; 把内存地址后移0x200 即512Byte
	ADD AX,0x0020
	MOV ES,AX ; 因为没有ADD ES,0x020指令，所以这里稍微绕个弯
	ADD CL,1 ; 往CL里加1
	CMP CL,18 ; 比较CL与18
	JBE readloop ; 如果CL <= 18跳转至readloop
```

JBE指令：”jump if below or equal“ 小于等于则跳转。

读入下一个扇区只要给CL + 1（要读入的盘号） 和给ES + 0x20（内存地址）即可

###### 为什么不把AL 设为 17 一些读入18个扇区？

> 指定处理的扇区数，范围在0x01～0xff（指 定0x02以上的数值时，要特别注意能够连 续处理多个扇区的条件。如果是FD的话，似 乎不能跨越多个磁道，也不能超过64KB的 界限。）这里暂不详细解释

我们已经把磁盘上 C0-H0-S2到C0-H0-S18的512×17=8 704字节 的内容，装载到了内存的0x8200～0xa3ff处。

##### 4.读入10个柱面

C0-H0- S18扇区的下一扇区，是磁盘反面的C0-H1-S1， 这次也从0xa400读入吧。按顺序读到C0-H1-S18 后，接着读下一个柱面C1-H0-S1。我们保持这个 势头，一直读到C9-H1-S18好了。

```assembly
;读磁盘
	MOV AX,0x0820
	MOV ES,AX
	MOV CH,0 ; 柱面0
	MOV DH,0 ; 磁头0
	MOV CL,2 ; 扇区2
readloop:
	MOV SI,0 ; 记录失败次数的寄存器
retry:
	MOV AH,0x02 ; AH=0x02 : 读入磁盘
	MOV AL,1 ; 1个扇区
	MOV BX,0
	MOV DL,0x00 ; A驱动器
	INT 0x13 ; 调用磁盘BIOS
	JNC next ; 没出错时跳转到next
	ADD SI,1 ; SI加1
	CMP SI,5 ; 比较SI与5
	JAE error ; SI >= 5时，跳转到error
	MOV AH,0x00
	MOV DL,0x00 ; A驱动器
	INT 0x13 ; 重置驱动器
	JMP retry
next:
	MOV AX,ES ; 把内存地址后移0x200
	ADD AX,0x0020
	MOV ES,AX ; 因为没有ADD ES,0x020指令，所以这里稍微绕个弯
	ADD CL,1 ; CL加1
	CMP CL,18 ; 比较CL与18 看看是否把扇区读完了
	JBE readloop ; 如果CL <=18，则跳转至readloop
	MOV CL,1
	ADD DH,1
	CMP DH,2	;看看是不是把正面读完了
	JB readloop ; 如果DH < 2，则跳转到readloop
	MOV DH,0
	ADD CH,1
	CMP CH,CYLS	;看看是不是读到了第10个扇区
	JB readloop ; 如果CH <CYLS，则跳转到readloop

```

JB指令：”jump if below“ 如果小于的话就跳转

EQU指令：CYLS EQU 10”意思是“CYLS = 10”。EQU 是“equal”的缩写。只将它定义成常数是因为以 后我们可能修改这个数字。CYLS是柱面英文的缩写

##### 5.确认操作系统的执行情况

以上内容可以总结为：一般向一个空软盘保存文件 时， 

1. 文件名会写在0x002600以后的地方； 
2. 文件的内容会写在0x004200以后的地方。

haribote.nas

```assembly
; haribote-os
; TAB=4
	ORG 0xc200 ; 0xc200 = 0x8000 + 0x4200 由于磁盘的内容被装载到了0x8000 所以系统的位置是0x4200
	MOV AL,0x13 ; VGA显卡，320x200x8位彩色
	MOV AH,0x00
	INT 0x10
fin:
	HLT
	JMP fin
```

0x10 调用BIOS设置显卡模式

> 设置显卡模式（video mode） 
>
> AH=0x00; 
>
> AL=模式：（省略了一些不重要的画面模 式） 
>
> 0x03：16色字符模式，80 × 25 
>
> 0x12：VGA 图形模式，640 × 480 × 4位彩色模式，独特的4面存储模 式 
>
> 0x13：VGA 图形模式，320 × 200 × 8位彩色模式，调色板模式 
>
> 0x6a：扩展VGA 图形模式，800 × 600 × 4位彩色模式，独特的4面存 储模式（有的显卡不支持这个模式）
>
> 返回值：无

##### 6.32位模式前期准备

haribote.nas

```assembly
; haribote-os
; TAB=4
; 有关BOOT_INFO
	CYLS EQU 0x0ff0 ; 设定启动区
	LEDS EQU 0x0ff1
	VMODE EQU 0x0ff2 ; 关于颜色数目的信息。颜色的位数。
	SCRNX EQU 0x0ff4 ; 分辨率的X（screen x）
	SCRNY EQU 0x0ff6 ; 分辨率的Y（screen y）
	VRAM EQU 0x0ff8 ; 图像缓冲区的开始地址
	ORG 0xc200 ; 这个程序将要被装载到内存的什么地方呢？
	MOV AL,0x13 ; VGA 显卡，320x200x8位彩色
	MOV AH,0x00
	INT 0x10
	MOV BYTE [VMODE],8 ; 记录画面模式
	MOV WORD [SCRNX],320
	MOV WORD [SCRNY],200
	MOV DWORD [VRAM],0x000a0000;用BIOS取得键盘上各种LED指示灯的状态
	MOV AH,0x02
	INT 0x16 ; keyboard BIOS
	MOV [LEDS],AL
fin:
	HLT
	JMP fin
```

如果用32位模式就不能调用BIOS功能了。 这是因为BIOS是用16位机器语言写的。如果我们 有什么事情想用BIOS来做，那就全部都放在开头先 做，因为一旦进入32位模式就不能调用BIOS函数 了。

设置画面模式之后，还把画 面模式的信息保存在了内存里。这是因为，以后我 们可能要支持各种不同的画面模式，这就需要把现 在的设置信息保存起来以备后用。我们暂且将启动 时的信息称为BOOT_INFO。INFO是英文 information（信息）的缩写。

##### 6.开始导入C语言

```c
void HariMain(void)
{
	fin:
	/*这里想写上HLT，但C语言中不能用HLT!*/
	goto fin;
}
```

目标文件是一种特殊的机器语言文件，必须与其他 文件链接（link）后才能变成真正可以执行的机器 语言。

##### 7.实现HLT

即如何在C语言中调用汇编函数。

naskfunc.nas

```assembly
; naskfunc
; TAB=4
	[FORMAT "WCOFF"] ; 制作目标文件的模式
	[BITS 32] ; 制作32位模式用的机械语言
;制作目标文件的信息
	[FILE "naskfunc.nas"] ; 源文件名信息
	GLOBAL _io_hlt ; 程序中包含的函数名
;以下是实际的函数
	[SECTION .text] ; 目标文件中写了这些之后再写
程序
	_io_hlt: ; void io_hlt(void);
	HLT
	RET
```

bootpack.c

```c
/*告诉C编译器，有一个函数在别的文件里*/
void io_hlt(void);
/*是函数声明却不用{ }，而用;，这表示的意思是：函数
是在别的文件中，你自己找一下吧！*/
void HariMain(void){
	fin:
	io_hlt(); /*执行naskfunc.nas里的_io_hlt*/
	goto fin;
}
```

